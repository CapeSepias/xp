apply plugin: 'java-base'
apply plugin: 'build-dashboard'
apply plugin: "com.github.jk1.dependency-license-report"

ext {
    leafProjects = subprojects.findAll { p -> p.subprojects.empty }
    javaProjects = leafProjects - project( ':runtime' ) - project( ':docs' )
    mavenPublishProjects = leafProjects
}

allprojects {
    group = 'com.enonic.xp'

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://repo.enonic.com/public'
        }
    }

    apply plugin: 'com.enonic.defaults'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jk1:gradle-license-report:1.12"
    }
}

configure( mavenPublishProjects ) {
    apply from: "$rootDir/gradle/maven-publish.gradle"
}

configure( javaProjects ) {
    apply from: "$rootDir/gradle/java.gradle"
}

configure( rootProject ) {
    apply from: "$rootDir/gradle/coverage.gradle"
    apply from: "$rootDir/gradle/ci.gradle"
    apply from: "$rootDir/gradle/tools.gradle"
}
