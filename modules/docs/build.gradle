apply plugin: 'java-base'
apply plugin: 'com.github.node-gradle.grunt'

node {
    download = true
    version = '8.12.0'
}

ext {
    leafProjects = rootProject.subprojects.findAll { p -> p.subprojects.empty }
    jsDocOutput = "${buildDir}/jsdoc"
}

def javadocProjects = leafProjects.findAll { project -> project.name.endsWith( '-api' ) }

task javadocAll( type: Javadoc, group: 'documentation' ) {
    failOnError = false
    title = "Enonic XP API ${version}"
    verbose = false

    options {
        links 'http://docs.oracle.com/javase/8/docs/api'
        links 'http://www.javadoc.io/doc/com.google.guava/guava/26.0-jre'
        quiet()
        encoding( 'UTF-8' )
    }

    javadocProjects.each { project ->
        project.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}

task javadocZip( type: Zip ) {
    from javadocAll
    archiveClassifier.set( 'javadoc' )
}

task gruntAll( type: GruntTask, dependsOn: npmInstall ) {
    description = 'Build JSDoc for libraries.'
    inputs.files fileTree( dir: "${projectDir}/../lib", include: '**/lib/xp/*.js' )
    inputs.dir "${projectDir}/src/jsdoc"
    outputs.dir jsDocOutput
    args = ['all']
}

task libdocZip( type: Zip ) {
    from gruntAll
    archiveClassifier.set( 'libdoc' )
}

publishing.publications {
    mavenJava( MavenPublication ) {
        artifact javadocZip
        artifact libdocZip
    }
}
